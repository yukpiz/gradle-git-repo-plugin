apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.7
group = 'me.yukpiz'
apply from: "$rootDir/version.gradle"

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'org.ajoberstar:gradle-git:1.0.0'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

def repo = new File(rootDir, "./maven")

uploadArchives {
    repositories {
        mavenDeployer {
            //beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            repository url: "file://${repo.absolutePath}"
            pom.groupId = "${group}"
            pom.project {
                name 'Gradle Git Repo Plugin'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Use a git repo as a maven-style repo'
                url 'https://github.com/yukpiz/gradle-git-repo-plugin'

                scm {
                    connection 'scm:git:git@github.com:yukpiz/gradle-git-repo-plugin.git'
                    developerConnection 'scm:git:git@github.com:yukpiz/gradle-git-repo-plugin.git'
                    url 'https://github.com/yukpiz/gradle-git-repo-plugin'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'drapp'
                        name 'Douglas Rapp'
                        email 'doug@layer.com'
                    }
                }
            }
        }
    }
}

